buildscript {
    ext.kotlin_version = '1.7.10'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "org.jetbrains.intellij" version "1.13.3"
    id "org.jetbrains.grammarkit" version "2022.3.1"
}

group 'org.cutejs'
version '1.1.2-223'

sourceCompatibility = "17"
targetCompatibility = "17"

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.intellij'

sourceSets {
    all {
        java.srcDirs += ['src/main/gen']
        kotlin.srcDirs += ['src/main/kotlin']
        resources.srcDirs = ['src/main/resources']
    }
}

grammarKit {
    jflexRelease = "1.7.0-1"
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.13.1'
    compileOnly fileTree(dir: '/opt/phpstorm/plugins/javascript-impl/lib', include: ['javascript-impl.jar', 'javascript-openapi.jar', 'resources_en.jar', 'js-test-common.jar'])
}

intellij {
    version = '2022.3'
    runIde { ideDir = file('/opt/phpstorm') }
    downloadSources = true
    pluginName = 'cutejs-intellij-plugin'
}

patchPluginXml {
	sinceBuild = "223"
    changeNotes = """
      <ul>
        <li>Support for 2022.3 Intellij platform.</li>
      </ul>
      """
}


apply plugin: 'org.jetbrains.grammarkit'

import org.jetbrains.grammarkit.tasks.GenerateLexerTask
import org.jetbrains.grammarkit.tasks.GenerateParserTask

def GENERATE_GROUP = 'Generate'

task generateCuteJSLexer(type: GenerateLexerTask) {
    sourceFile = file('src/main/grammar/CuteJS.flex')
    targetDir = 'src/main/gen/org/cutejs/lang/lexer'
    targetClass = "_CuteLexer"
    purgeOldFiles = true

    description = 'Generate Lexer Java sources for CuteJS'
    group = GENERATE_GROUP
}

task generateCuteJSParser(type: GenerateParserTask) {
    sourceFile = file('src/main/grammar/CuteJS.bnf')
    targetRoot = 'src/main/gen'
    pathToParser = 'src/main/gen/org/cutejs/lang/CuteParser.java'
    pathToPsiRoot = 'src/main/gen/org/cutejs/lang/psi'
    purgeOldFiles = true

    description = 'Generate Parser Java sources for CuteJS'
    group = GENERATE_GROUP

    // patch up to date check
    outputs.upToDateWhen { false }
}

compileKotlin {
    kotlinOptions.jvmTarget = "17"
}
compileJava {
    dependsOn generateCuteJSLexer
    dependsOn generateCuteJSParser
}

compileKotlin {
    dependsOn generateCuteJSLexer
    dependsOn generateCuteJSParser
}