buildscript {
    ext.kotlin_version = '1.3.72'

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io'}
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            url 'https://dl.bintray.com/jetbrains/intellij-plugin-service'
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.4.18"
    id "org.jetbrains.grammarkit" version "2020.1.2"
}

group 'org.cutejs'
version '1.0.4'

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.intellij'

sourceSets {
    all {
        java.srcDirs += ['src/main/gen']
        kotlin.srcDirs += ['src/main/kotlin']
        resources.srcDirs = ['src/main/resources']
    }
}

sourceCompatibility = 1.8

grammarKit {
    jflexRelease = '1.7.0'
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: '1.3.72'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    implementation fileTree(dir: '/opt/webstorm/plugins/JavaScriptLanguage/lib', include: ['JavaScriptLanguage.jar', 'javascript-openapi.jar', 'resources_en.jar', 'js-test-common.jar'])
}

intellij {
    version '2020.1'
    alternativeIdePath = '/opt/webstorm'
    downloadSources = false
    pluginName = 'cutejs-intellij-plugin'
}

patchPluginXml {
    changeNotes """
      <ul>
        <li>Support for 2020.1</li>
      </ul>
      """
}

import org.jetbrains.grammarkit.tasks.*

def GENERATE_GROUP = 'Generate'

task generateLexer(type: GenerateLexer) {
    source = "src/main/grammar/CuteJS.flex"
    targetDir = "src/main/gen/org/cutejs/lang/lexer"
    targetClass = "_CuteLexer"
    skeleton = "src/main/grammar/idea-flex.skeleton"
    purgeOldFiles = true

    description = 'Generate Lexer Java sources for CuteJS'
    group = GENERATE_GROUP
}

task generateParser(type: GenerateParser) {
    source = "src/main/grammar/CuteJS.bnf"
    targetRoot = 'src/main/gen'
    pathToParser = 'src/main/gen/org/cutejs/lang/CuteParser.java'
    pathToPsiRoot = 'src/main/gen/org/cutejs/lang/psi'
    purgeOldFiles = true

    description = 'Generate Parser Java sources for CuteJS'
    group = GENERATE_GROUP

    // patch up to date check
    outputs.upToDateWhen { false }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileKotlin.dependsOn(generateLexer, generateParser)
